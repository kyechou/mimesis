pkgname=klee-libc++
pkgver=10.0.0
pkgrel=1
pkgdesc='LLVM C++ standard library for KLEE.'
arch=('i686' 'x86_64')
url="https://libcxx.llvm.org/"
license=('custom:Apache 2.0 with LLVM Exception')
depends=('cxx-common=0.0.14')
makedepends=('libunwind' 'ninja' 'python' 'gllvm-git')
provides=('libc++' 'libc++abi' 'libc++experimental')
conflicts=('libc++' 'libc++abi' 'libc++experimental')
options=('staticlibs' '!strip')
source=("https://github.com/llvm/llvm-project/releases/download/llvmorg-$pkgver/llvm-$pkgver.src.tar.xz"
        "https://github.com/llvm/llvm-project/releases/download/llvmorg-$pkgver/libcxx-$pkgver.src.tar.xz"
        "https://github.com/llvm/llvm-project/releases/download/llvmorg-$pkgver/libcxxabi-$pkgver.src.tar.xz")
noextract=("${source[@]##*/}")
sha256sums=('df83a44b3a9a71029049ec101fb0077ecbbdf5fe41e395215025779099a98fdf'
            '270f8a3f176f1981b0f6ab8aa556720988872ec2b48ed3b605d0ced8d09156c7'
            'e71bac75a88c9dde455ad3f2a2b449bf745eafd41d2d8432253b2964e0ca14e1')

prepare() {
    mkdir -p build llvm/projects/libcxx llvm/projects/libcxxabi
    bsdtar --strip-components 1 -zxf "${source[0]##*/}" -C llvm
    bsdtar --strip-components 1 -zxf "${source[1]##*/}" -C llvm/projects/libcxx
    bsdtar --strip-components 1 -zxf "${source[2]##*/}" -C llvm/projects/libcxxabi
    sed -i 's/CREDITS.TXT/CREDITS/' llvm/projects/libcxx/LICENSE.TXT llvm/projects/libcxxabi/LICENSE.TXT
    sed -i 's/\.hasWarningFlag/\.hasCompileFlag/' llvm/projects/libcxx/utils/libcxx/{compiler.py,test/config.py}
}

build() {
    export TRAILOFBITS_LIBRARIES="/opt/cxx-common/libraries"
    export PATH="${TRAILOFBITS_LIBRARIES}/cmake/bin:${TRAILOFBITS_LIBRARIES}/llvm/bin:${PATH}"

    cd "$srcdir/build"
    # https://wiki.archlinux.org/index.php/Clang#Build_packages_with_Clang
    CFLAGS=${CFLAGS/-fvar-tracking-assignments}
    CXXFLAGS=${CXXFLAGS/-fvar-tracking-assignments}
    export CC=gclang
    export CXX=gclang++
    export LLVM_COMPILER_PATH="${TRAILOFBITS_LIBRARIES}/llvm/bin"

    "${TRAILOFBITS_LIBRARIES}/cmake/bin/cmake" \
        -G Ninja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_C_COMPILER=gclang \
        -DCMAKE_CXX_COMPILER=gclang++ \
        -DLIBCXX_INSTALL_EXPERIMENTAL_LIBRARY=NO \
        "$srcdir/llvm"
    ninja cxx cxxabi cxx_experimental

    local libraries=("$srcdir"/build/lib/lib*.{a,so*})
    for p in "${libraries[@]}"; do
        filetype="$(file "$p")"
        if [[ "$filetype" == *"ar archive"* || "$filetype" == *"shared object"* ]]; then
            get-bc "$p"
        fi
    done
}

check() {
    export TRAILOFBITS_LIBRARIES="/opt/cxx-common/libraries"
    export PATH="${TRAILOFBITS_LIBRARIES}/cmake/bin:${TRAILOFBITS_LIBRARIES}/llvm/bin:${PATH}"

    cd "$srcdir/build"
    ninja check-cxx check-cxxabi
}

package() {
    ## install libc++
    cd "$srcdir/build"
    DESTDIR="$pkgdir" ninja install-libcxx

    install -Dm0644 -t "$pkgdir"/usr/share/"$pkgname"/usr/lib/ "$srcdir"/build/lib/libc++.bca
    install -Dm0644 -t "$pkgdir"/usr/share/"$pkgname"/usr/lib/ "$srcdir"/build/lib/libc++.so*.bc
    ln -sf libc++.so.1.0.bc "$pkgdir"/usr/share/"$pkgname"/usr/lib/libc++.so.bc
    ln -sf libc++.so.1.0.bc "$pkgdir"/usr/share/"$pkgname"/usr/lib/libc++.so.1.bc
    install -Dm0644 "$srcdir"/llvm/projects/libcxx/CREDITS.TXT "$pkgdir"/usr/share/licenses/"$pkgname"/CREDITS
    install -Dm0644 "$srcdir"/llvm/projects/libcxx/LICENSE.TXT "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE

    ## install libc++abi
    cd "$srcdir/build"
    DESTDIR="$pkgdir" ninja install-libcxxabi

    install -Dm0644 -t "$pkgdir"/usr/share/"$pkgname"/usr/lib/ "$srcdir"/build/lib/libc++abi.bca
    install -Dm0644 -t "$pkgdir"/usr/share/"$pkgname"/usr/lib/ "$srcdir"/build/lib/libc++abi.so*.bc
    ln -sf libc++abi.so.1.0.bc "$pkgdir"/usr/share/"$pkgname"/usr/lib/libc++abi.so.bc
    ln -sf libc++abi.so.1.0.bc "$pkgdir"/usr/share/"$pkgname"/usr/lib/libc++abi.so.1.bc
    mkdir -p "$pkgdir"/usr/src/ && cp -r "$srcdir"/llvm/projects/libcxxabi "$pkgdir"/usr/src/
    install -Dm0644 "$srcdir"/llvm/projects/libcxxabi/CREDITS.TXT "$pkgdir"/usr/share/licenses/"$pkgname"/CREDITS
    install -Dm0644 "$srcdir"/llvm/projects/libcxxabi/LICENSE.TXT "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE

    ## install libc++experimental
    install -Dm0644 -t "$pkgdir"/usr/lib/ "$srcdir"/build/lib/libc++experimental.a
    install -Dm0644 -t "$pkgdir"/usr/share/"$pkgname"/usr/lib/ "$srcdir"/build/lib/libc++experimental.bca
    install -Dm0644 "$srcdir"/llvm/projects/libcxx/CREDITS.TXT "$pkgdir"/usr/share/licenses/"$pkgname"/CREDITS
    install -Dm0644 "$srcdir"/llvm/projects/libcxx/LICENSE.TXT "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
