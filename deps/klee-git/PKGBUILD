pkgname=klee-git
pkgver=2.2.r0.g5719d280
pkgrel=1
pkgdesc="Symbolic virtual machine built on top of the LLVM compiler infrastructure"
arch=('x86_64')
url="https://klee.github.io/"
license=('custom:UIUC')
groups=()
depends=('cxx-common=0.1.1' 'libcap' 'sqlite' 'gperftools' 'zlib' 'klee-uclibc'
         'klee-libc++' 'python' 'python-tabulate' 'lib32-glibc' 'lib32-gcc-libs')
makedepends=('git' 'cmake')
checkdepends=()
optdepends=()
provides=('klee=2.2')
conflicts=('klee')
source=("$pkgname"::"git+https://github.com/klee/klee.git#commit=5719d2803e93252e5d4613f43afc7db0d72332f1"
        "git+https://github.com/google/googletest.git#tag=release-1.10.0"
        '00-ret-void.patch')
sha256sums=('SKIP'
            'SKIP'
            '79d25bfbc3cf72dd4293ed75c75ae9043a849360708db99654068060585a3147')

prepare() {
    vcpkg_libs='/opt/cxx-common/installed/x64-linux-rel'

    ## find Z3 from cxx-common
    sed -i "$srcdir/$pkgname/cmake/find_z3.cmake" \
        -e "s|find_package(Z3)|find_package(Z3 PATHS ${vcpkg_libs})|"

    cd "$srcdir/$pkgname"
    patch -Np1 -i "$srcdir/00-ret-void.patch"
}

pkgver() {
    cd "$srcdir/$pkgname"
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    vcpkg_libs='/opt/cxx-common/installed/x64-linux-rel'
    export PATH="$vcpkg_libs/bin:${PATH}"

    mkdir -p "$srcdir/build"
    cd "$srcdir/build"
    cmake \
        -DCMAKE_C_COMPILER="$vcpkg_libs/bin/clang" \
        -DCMAKE_CXX_COMPILER="$vcpkg_libs/bin/clang++" \
        -DENABLE_TCMALLOC=ON \
        -DENABLE_POSIX_RUNTIME=ON \
        -DENABLE_KLEE_UCLIBC=ON \
        -DKLEE_UCLIBC_PATH="/usr/share/klee-uclibc/usr" \
        -DENABLE_KLEE_LIBCXX=ON \
        -DKLEE_LIBCXX_DIR="/usr/share/klee-libc++/usr" \
        -DKLEE_LIBCXX_INCLUDE_DIR="/usr/include/c++/v1" \
        -DKLEE_LIBCXXABI_SRC_DIR="/usr/src/libcxxabi" \
        -DENABLE_SOLVER_Z3=ON \
        -DENABLE_SOLVER_STP=OFF \
        -DENABLE_SOLVER_METASMT=OFF \
        -DZ3_INCLUDE_DIRS="$vcpkg_libs/include" \
        -DZ3_LIBRARIES="$vcpkg_libs/lib/libz3.a" \
        -DCMAKE_INSTALL_PREFIX="/usr" \
        -DCMAKE_INSTALL_LIBDIR="/usr/lib" \
        -DCMAKE_BUILD_TYPE=Release \
        -DGTEST_SRC_DIR="$srcdir/googletest/googletest" \
        -DENABLE_UNIT_TESTS=ON \
        -DENABLE_SYSTEM_TESTS=ON \
        -DCMAKE_VERBOSE_MAKEFILE=True \
        "$srcdir/$pkgname"
    make
}

check() {
    cd "$srcdir/build"
    make systemtests
}

package() {
    cd "$srcdir/build"
    make DESTDIR="$pkgdir/" install
    install -Dm644 "$srcdir/$pkgname/LICENSE.TXT" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: set sw=4 ts=4 et:
