pkgname=klee-git
pkgver=v2.1.r168.g53ace1a1
pkgrel=1
epoch=
pkgdesc="Symbolic virtual machine built on top of the LLVM compiler infrastructure"
arch=('x86_64')
url="https://klee.github.io/"
license=('custom:UIUC')
groups=()
depends=('cxx-common=0.0.14' 'libcap' 'ncurses' 'sqlite' 'gperftools' 'zlib'
         'python' 'klee-uclibc' 'klee-libc++' 'lib32-glibc' 'lib32-gcc-libs')
makedepends=('git' 'llvm10') # llvm10: for testing (lit)
checkdepends=('python-tabulate')
optdepends=()
provides=('klee=2.1')
conflicts=('klee')
replaces=()
backup=()
options=()
install=
changelog=
source=("$pkgname"::"git+https://github.com/klee/klee.git#commit=53ace1a119c5ede520d2e597f9bcdfeece31229a"
        "git+https://github.com/google/googletest.git#tag=release-1.7.0")
noextract=()
sha256sums=('SKIP'
            'SKIP')
validpgpkeys=()

pkgver() {
    cd "$srcdir/$pkgname"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    export TRAILOFBITS_LIBRARIES="/opt/cxx-common/libraries"
    export PATH="${TRAILOFBITS_LIBRARIES}/cmake/bin:${TRAILOFBITS_LIBRARIES}/llvm/bin:${PATH}"

    mkdir -p "$srcdir/build"
    cd "$srcdir/build"
    "${TRAILOFBITS_LIBRARIES}/cmake/bin/cmake" \
        -DCMAKE_C_COMPILER="${TRAILOFBITS_LIBRARIES}/llvm/bin/clang" \
        -DCMAKE_CXX_COMPILER="${TRAILOFBITS_LIBRARIES}/llvm/bin/clang++" \
        -DENABLE_TCMALLOC=ON \
        -DENABLE_POSIX_RUNTIME=ON \
        -DENABLE_KLEE_UCLIBC=ON \
        -DKLEE_UCLIBC_PATH="/usr/share/klee-uclibc/usr" \
        -DENABLE_KLEE_LIBCXX=ON \
        -DKLEE_LIBCXX_DIR="/usr/share/klee-libc++/usr" \
        -DKLEE_LIBCXX_INCLUDE_DIR="/usr/include/c++/v1" \
        -DKLEE_LIBCXXABI_SRC_DIR="/usr/src/libcxxabi" \
        -DENABLE_SOLVER_Z3=ON \
        -DENABLE_SOLVER_STP=OFF \
        -DENABLE_SOLVER_METASMT=OFF \
        -DZ3_ROOT="${TRAILOFBITS_LIBRARIES}/z3" \
        -DCMAKE_INSTALL_PREFIX="/usr" \
        -DCMAKE_INSTALL_LIBDIR="/usr/lib" \
        -DCMAKE_BUILD_TYPE=Release \
        -DGTEST_SRC_DIR="$srcdir/googletest" \
        -DENABLE_UNIT_TESTS=ON \
        -DENABLE_SYSTEM_TESTS=ON \
        "$srcdir/$pkgname"
    make
}

check() {
    export TRAILOFBITS_LIBRARIES="/opt/cxx-common/libraries"
    export PATH="${TRAILOFBITS_LIBRARIES}/cmake/bin:${TRAILOFBITS_LIBRARIES}/llvm/bin:${PATH}"

    cd "$srcdir/build"
    make systemtests
    make unittests
}

package() {
    cd "$srcdir/build"
    make DESTDIR="$pkgdir/" install
    install -Dm644 "$srcdir/$pkgname/LICENSE.TXT" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: set sw=4 ts=4 et:
